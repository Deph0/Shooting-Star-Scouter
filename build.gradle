plugins {
	id 'java'
	id 'eclipse'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
		content {
			includeGroupByRegex("net\\.runelite.*")
		}
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	implementation 'org.java-websocket:Java-WebSocket:1.5.3'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'com.shootingstar.scouter'
version = '1.0-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('shadowJar', Jar) {
	dependsOn configurations.testRuntimeClasspath
	manifest {
		attributes('Main-Class': 'com.shootingstar.scouter.ShootingStarScouterPluginTest', 'Multi-Release': true)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from sourceSets.main.output
	from sourceSets.test.output
	from {
		configurations.testRuntimeClasspath.collect { file ->
			file.isDirectory() ? file : zipTree(file)
		}
	}

	exclude 'META-INF/INDEX.LIST'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude '**/module-info.class'

	group = BasePlugin.BUILD_GROUP
	archiveClassifier.set('shadow')
	archiveFileName.set("${rootProject.name}-${project.version}-all.jar")
}

tasks.register('runPlugin', JavaExec) {
	dependsOn testClasses
	
	classpath = sourceSets.test.runtimeClasspath
	mainClass.set('com.shootingstar.scouter.ShootingStarScouterPluginTest')
	
	jvmArgs = [
		'-ea',
		'--add-opens', 'java.desktop/sun.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/java.awt.event=ALL-UNNAMED',
		'--add-opens', 'java.desktop/javax.swing=ALL-UNNAMED',
		'--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED'
	]
	
	args = ['--debug', '--developer-mode']
	
	group = 'application'
	description = 'Runs the Shooting Star Scouter plugin in RuneLite'
}
